module BankingExample1;

interface BankAccountI {
  Int getId();
  [Secrecy: High] Int getBalance();
  Unit depositMoney([Secrecy: High] Int amount);
}

interface UserAccountI {
  [Secrecy: High] Bool tryLogin(Int userID,[Secrecy: High] String password);
  BankAccountI getBankAccount(Int number);
}

interface BankI {
  [Secrecy: High] UserAccountI login(Int userID,[Secrecy: High] String password);
}

class BankAccount(Int accountId) implements BankAccountI {
  [Secrecy: High] Int balance = 0;
  Int id = accountId;

  //meant to pass
  Int getId() {
      return id;
  }

  //meant to pass
  [Secrecy: High] Int getBalance() {
      return balance;
  }

  //meant to pass
  Unit depositMoney([Secrecy: High] Int amount) {
      this.balance = this.balance - amount;
  }
}


class UserAccount(Int userId, String userPassword) implements UserAccountI {
  Int userID = userId;
  [Secrecy: High] String password = userPassword;
  [Secrecy:High] Int incorrectLogins = 0;
  List<BankAccountI> bankAccounts = Nil;

  //meant to pass
  [Secrecy:High] Bool tryLogin(Int userID,[Secrecy: High] String password) {
    
    Bool userIDCorrect = (this.userID == userID);
    [Secrecy:High] Bool pwdCorrect = (strlen(this.password) == strlen(password));
    [Secrecy:High] Int i = 0;

    while(i < strlen(password) && pwdCorrect) {
      [Secrecy:High] String thisChar = substr(this.password, i, i + 1);
      [Secrecy:High] String paramChar = substr(password, i, i + 1);
      [Secrecy:High] Bool charsMatch = (thisChar == paramChar);
      
      if (!charsMatch) {
          pwdCorrect = False;
      }
      
      i = i + 1;
    }

    [Secrecy:High] Bool incorrectLoginsInRange = (0 <= incorrectLogins && incorrectLogins < 3);

    if(userIDCorrect && incorrectLoginsInRange) {
      if (pwdCorrect) {
        this.incorrectLogins = 0;
      } else {
        this.incorrectLogins = this.incorrectLogins + 1;
      }
    }
    return userIDCorrect && pwdCorrect && incorrectLoginsInRange;
  }

  //meant to pass
  BankAccountI getBankAccount(Int number) {
    BankAccountI resultAccount = null;

    if(number >= 0 && length(bankAccounts) > number) {
      resultAccount = nth(bankAccounts, number);
    }
    return resultAccount;
  }
}


class Bank implements BankI {
  List<UserAccountI> userAccounts = Nil;

  //meant to pass
  [Secrecy: High] UserAccountI login(Int userID,[Secrecy: High] String password) {
    [Secrecy: High] UserAccountI accountTried = null;

    if (0 <= userID && userID < length(userAccounts)) {
      accountTried = nth(userAccounts, userID);

      [Secrecy: High] Bool loginSuccess = accountTried.tryLogin(userID, password);

      if (!loginSuccess) {
        accountTried =  null;
      }
      
    } else {
      accountTried =  null;
    }

    return accountTried;
  }
}
